plugins {
    id 'org.springframework.boot' version '2.4.5'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

static def getGitVersionCode() {
    // eg: 36
    try {
        return 'git rev-list HEAD --first-parent --count'.execute().text.trim().toInteger()
    } catch (ignored) {
        return 1
    }
}

static def getGitVersionInfo() {
    // eg: 60d0ed1
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

static def getGitBranch() {
    // eg: master
    return 'git symbolic-ref --short -q HEAD'.execute().text.trim()
}

group = 'com.scrat'
version = '0.0.1.' + getGitVersionCode()
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

springBoot {
    buildInfo()
}

ext {
    set('springCloudVersion', "2020.0.2")
}

dependencies {
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-server'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

test {
    useJUnitPlatform()
}
